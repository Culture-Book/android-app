plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    signingConfigs {
        debug {
            storeFile file('./debug.keystore')
            storePassword System.getenv("DEBUG_PASS")
            keyAlias System.getenv("DEBUG_ALIAS")
            keyPassword System.getenv("DEBUG_PASS")
        }
        pre_release {
            storeFile file('./pre_release.keystore')
            storePassword System.getenv("PRE_RELEASE_PASS")
            keyAlias System.getenv("PRE_RELEASE_ALIAS")
            keyPassword System.getenv("PRE_RELEASE_PASS")
        }
        release {
            storeFile file('./release.keystore')
            storePassword System.getenv("RELEASE_PASS")
            keyAlias System.getenv("RELEASE_ALIAS")
            keyPassword System.getenv("RELEASE_PASS")
        }
    }
    namespace "$app_id"
    compileSdk "$target_sdk".toInteger()

    defaultConfig {
        applicationId "io.culturebook"
        minSdk "$min_sdk".toInteger()
        targetSdk "$target_sdk".toInteger()
        versionCode version_code
        versionName "Culturebook v${majorRelease}.${minorRelease}.${buildNumber}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.debug
            minifyEnabled false
        }
        pre_release_debug {
            applicationIdSuffix ".pre_release"
            versionNameSuffix '-PRE_RELEASE'
            debuggable true
            signingConfig signingConfigs.pre_release
            minifyEnabled true
            matchingFallbacks = ['pre_release']
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        pre_release {
            applicationIdSuffix ".pre_release"
            versionNameSuffix '-PRE_RELEASE'
            signingConfig signingConfigs.pre_release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation project(":app:core:ui")
    implementation project(":app:core:data")
    implementation project(":app:core:nav")
    implementation project(":app:features:auth")
    implementation project(":app:features:home")

    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation "androidx.core:core-ktx:$android_core"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_runtime"
    implementation "androidx.activity:activity-compose:$activity_compose"
}